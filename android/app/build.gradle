apply plugin: "com.android.application"

import com.android.build.OutputFile

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"
apply from: "../../node_modules/react-native-code-push/android/codepush.gradle"

File f1 = new File(rootProject.projectDir, "amply.properties")
Properties props = new Properties()
props.load(new FileInputStream(f1))

repositories {
    maven { url  "http://jcenter.bintray.com" }
}

def enableSeparateBuildPerCPUArchitecture = true
def enableProguardInReleaseBuilds = false
def enableProguardInDebugBuilds = false

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.ecdrn"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 203
        versionName "2.0.3"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    signingConfigs {
        release {
            storeFile file(props['security.release.file'])
            keyAlias props['security.release.keyAlias']
            keyPassword props['security.release.keyPassword']
            storePassword props['security.release.storePassword']
        }
        debug {
            storeFile file(props['security.debug.file'])
            keyAlias props['security.debug.keyAlias']
            keyPassword props['security.debug.keyPassword']
            storePassword props['security.debug.storePassword']
        }
    }

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            buildConfigField "String", "CODEPUSH_KEY", props['codepush.release.key']
        }

        releaseStaging {
            initWith(buildTypes.release)
            applicationIdSuffix ".staging"
            buildConfigField "String", "CODEPUSH_KEY", props['codepush.staging.key']
        }

        debug {
            debuggable true
            minifyEnabled enableProguardInDebugBuilds
            buildConfigField "String", "CODEPUSH_KEY", '""'
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
}

dependencies {
    compile project(':appcenter-crashes')
    compile project(':appcenter-analytics')
    compile project(':appcenter')
    compile project(':react-native-vector-icons')
    compile project(':react-native-navigation')
    compile project(':react-native-google-analytics-bridge')
    compile project(':react-native-code-push')
    compile 'com.android.support:multidex:1.0.3'
    compile "com.facebook.react:react-native:+"
    compile 'com.madgag.spongycastle:core:1.54.0.0'
    compile 'com.madgag.spongycastle:prov:1.54.0.0'
    compile fileTree(dir: "libs", include: ["*.jar"])
}
configurations.all {
    resolutionStrategy {
        force 'com.android.support:multidex:1.0.3'
    }
}
// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}
