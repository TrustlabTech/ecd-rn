import groovy.json.JsonSlurper
def packageSlurper = new JsonSlurper()
def packageJson = packageSlurper.parse file('../../package.json')
def runtimeCfgJson = packageSlurper.parse file('../../cfg/runtime.json')

println "Building ECDRN v"+packageJson.version

apply plugin: "com.android.application"

import com.android.build.OutputFile

// react
project.ext.react = [
    bundleAssetName: "index.android.bundle",
    entryFile: "index.android.js",
    bundleInRelease: true,
    jsBundleDirRelease: "$buildDir/intermediates/assets/release",
    resourcesDirRelease: "$buildDir/intermediates/res/merged/release"
]

apply from: "../../node_modules/react-native/react.gradle"

// react-native-vector-icons
apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"

def enableSeparateBuildPerCPUArchitecture = true
def enableProguardInReleaseBuilds = true


android {
    // lintOptions {
    //     checkReleaseBuilds false
    // }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.ecdrn"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName packageJson.version
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    signingConfigs {
        release {
            storeFile file('ECDRN.keystore')
            storePassword 'shantaram'
            keyAlias 'ECDRN_RELEASE'
            keyPassword 'shantaram'
        }
        debug {
            storeFile file('ECDRN.keystore')
            storePassword 'shantaram'
            keyAlias 'ECDRN_RELEASE'
            keyPassword 'shantaram'
        }
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            applicationIdSuffix runtimeCfgJson.UNSTABLE ? ".unstable" : ""
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
}
repositories {
    maven {
        url  "http://jcenter.bintray.com"
    }
}

dependencies {
    compile project(':react-native-linear-gradient')
    compile project(':react-native-google-analytics-bridge')
    compile "com.android.support:appcompat-v7:23.0.1"
    compile 'com.joshdholtz.sentry:sentry-android:1.5.0'
    compile "com.facebook.react:react-native:+"
    compile fileTree(dir: "libs", include: ["*.jar"])
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
  from configurations.compile
  into 'libs'
}
